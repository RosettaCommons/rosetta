# -*- mode:python;indent-tabs-mode:nil;show-trailing-whitespace:t; -*-
#
# SCons build script for sources.
# (c) Copyright Rosetta Commons Member Institutions.
# (c) This file is part of the Rosetta software suite and is made available under license.
# (c) The Rosetta software is developed by the contributing members of the Rosetta Commons.
# (c) For more information, see http://www.rosettacommons.org. Questions about this can be
# (c) addressed to University of Washington UW TechTransfer, email: license@u.washington.edu.

# SCons imports
Import("build", "project")

# Python imports
from tools.build.settings import Settings, ProjectSettings
import os

# Load the project settings.
if project == "devel" :
  #if project is devel chose the approiate settings file to load.
  if "my" in map( str, BUILD_TARGETS) or "my_pilot_apps" in map( str, BUILD_TARGETS) :
    #if my or my_pilot_apps is specified build just devel source listed in src/devel.src.settings
    requested = Settings.load(build.toplevel + "/src/" + project + ".src.settings.my")
  else :
    #By default build all devel sources
    requested = Settings.load(build.toplevel + "/src/" + project + ".src.settings")
else :
  requested = Settings.load(build.toplevel + "/src/" + project + ".src.settings")

requested = ProjectSettings(project, requested)

# Add the default settings for source builds.
actual = ProjectSettings(project)

# Subprojects are used to determine dependencies on other projects
actual.subprojects = requested.subprojects

# Sources.  Currently we assume they are all C++.  If this ever changes
# we will need to differentiate language types in the .src.settings files.
for package, modules in requested.sources.items():
    for module in modules:
        # CUDA files have extension '.cu'
        if module.endswith(".cu"):
            #print "build.options.extras", build.options.extras
            if "cuda" not in build.options.extras : continue # skip .cu files if the "cuda" extra is not supplied
            source = module
        else:
            source = module + ".cc"
        if package:
            actual.sources += [ "%s/%s" % (package, source) ]
        else:
            actual.sources += [ "%s" % (source) ]


# Include paths.  Gives priority to user defined paths.
actual.include_path = \
    requested.include_path + \
    [ "#src" ] + \
    [ "#external/include" ] + \
    [ "#src/platform/" + "/".join(build.platform_includes) ]

# Searches the platform paths by moving up from most specific to most general.
# This makes more specific platforms take priority.
for last in range(1, len(build.platform_includes)):
    platform_include_path = "#src/platform/" + \
                            "/".join(build.platform_includes[:-last])
    actual.include_path += [ platform_include_path ]

# Library paths.  Gives priority to user defined paths.
actual.library_path = \
    requested.library_path + \
    [ "#external/lib" ] + \
    [ "#build/src/" + build.platform ] + \
    [ "#build/external/" + build.platform ]

# Libraries.  Gives priority to user defined libraries.
actual.libraries = \
    requested.libraries + \
    requested.subprojects

# The Boost library extensions vary by OS which is a huge nuisance.
# XXX: This is a temporary solution: the right answer is a Boost
# XXX: build script which does the work of selecting the extension.
boost_ext = {
    "linux" : "-gcc",
    # "macos" : "",
    # "windows" : "",
    }.get(build.options.os, "")
if "mingw" in build.options.extras and build.options.os == "windows":
    boost_ext = "-mgw"
pre_boostified = actual.libraries
actual.libraries = []
for lib in pre_boostified:
    if lib.startswith("boost_"):
        lib += boost_ext
    actual.libraries += [ lib ]


# Transform the modified settings into SCons Environment variables.
# Gives priority to project settings over global settings.
env = build.environment.Clone()
env.Prepend(**actual.symbols())
if "static" not in build.options.extras:
  env["RPATH"] = [
      build.toplevel + "/build/external/" + build.platform,
      env.Literal("\\$$ORIGIN"),
      env.Literal(os.path.join("\\$$ORIGIN", os.pardir, "lib")),
  ]
# direct SCons to the nvcc tool for CUDA source
env.Tool('nvcc', toolpath = ['#tools/build/'])



target = project
if "static" not in build.options.extras:
    #print "In SConscript.src"
    #print "actual.sources:"
    #print actual.sources
    #print "target"
    #print target

    lib = env.SharedLibrary(target, actual.sources)
    # Lacking this, doing a build of '<project>' won't build the library,
    # because the library is created above the '<project>' directory.
    Alias(project, lib)
else:
    lib = env.StaticLibrary(target, actual.sources)
    Alias(project, lib)
Default(project)

# Install the library in an easily accessible location
# Install("#lib", target)

# Setup useful aliases for building subdirectories of project.
# This allows a target to be project + package and have SCons build only
# that package.
# TODO: Allow building of specific source units.
for package in map(str, BUILD_TARGETS) :
    if package.startswith(project):
        Alias(package, "#build/src/" + build.platform + "/" + package)

